`*-----------------------------------*`
`*             >>>Pico<<<            *`
`*            Theo D'Hondt           *`
`*  VUB Programming Technology Lab   *`
`*              (c)1997              *`
`*-----------------------------------*`
{ eval(Exp):
    error('evaluator not initialized');
        
  init_eval(Dct):
    { DCT: Dct;

      eval_variable(Exp):
        { nam: Exp[VAR_NAM_idx];
          get(nam, DCT) };

      call_function(Exp, Dct):
        { dct: DCT;
          DCT:= Dct;
          val: eval(Exp);
          DCT:= dct;
          val };

      bind_variable(Exp, Var, Dct):
        { nam: Var[VAR_NAM_idx];
          val: eval(Exp);
          add(nam, val, Dct) };

      bind_application(Exp, Apl, Dct):
        { nam: Apl[APL_NAM_idx];
          arg: Apl[APL_ARG_idx]; 
          fun: FUN(nam, arg, Exp, DCT);
          add(nam, fun, Dct) };

      bind_error(Tab, Err, Dct):
        error('illegal parameter');
   
      bind_case: case(VAR_tag => bind_variable,
                      APL_tag => bind_application,
                         else => bind_error);
  
      call_table(TbA, TbP, Dct):
        { tbA: TbA[TAB_TAB_idx];
          tbP: TbP[TAB_TAB_idx];
          siz: size(tbA);
          if(siz = size(tbP),
             for(k: 1, k:= k+1, not(k > siz),
                 { arg: tbA[k];
                   par: tbP[k];
                   tag: par[TAG_idx];
                   cas: bind_case(tag);
                   Dct:= cas(arg, par, Dct) }),
             error('Illegal argument count'));
          Dct };
      
      call_multitable(TbA, TbP, Dct):
        display@['NEED TO COMPLETE FUNCTION call_multitable IN Eval.pco'];

      call_variable(Tab, Var, Dct):
        { nam: Var[VAR_NAM_idx];
          tab: Tab[TAB_TAB_idx];
          siz: size(tab);
          exp: if(siz > 0,
                  { idx: 0;
                    arg[siz]: eval(tab[idx:= idx+1]);
                    TAB(arg) },
                  Empty);
          add(nam, exp, Dct) };
   
      call_error(Tab, Apl, Dct):
        error('illegal parameter');
   
      call_case: case(TAB_tag => call_table,
                      `Add multitabulation support`
                      MTB_tag => call_multitable,
                      VAR_tag => call_variable,
                         else => call_error);
  
      eval_call(Fun, Tab):
        { par: Fun[FUN_PAR_idx];
          exp: Fun[FUN_EXP_idx];
          dct: Fun[FUN_DCT_idx];
          tag: par[TAG_idx];
          cas: call_case(tag);
          `display@['DEBUG eval_call(Fun, Tab): CAS(stuff)'];`
          dct:= cas(Tab, par, dct);
          call_function(exp, dct) };

      eval_application(Exp):
        { nam: Exp[APL_NAM_idx];
          arg: Exp[APL_ARG_idx];
          exp: get(nam, DCT);
          if(exp[TAG_idx] = FUN_tag,
             if(arg[TAG_idx] = TAB_tag,
                eval_call(exp, arg),
                { val: eval(arg);
                  if(val[TAG_idx] = TAB_tag,
                     eval_call(exp, val),
                     error('illegal argument')) }),
             if(exp[TAG_idx] = NAT_tag,
                { nat: exp[NAT_NAT_idx]; 
                  nat@arg },
                error('(native) function required'))) };

      eval_tabulation(Exp):
        { nam: Exp[TBL_NAM_idx];
          exp: get(nam, DCT);
          if(exp[TAG_idx] = TAB_tag,
             { idx: Exp[TBL_IDX_idx];
               val: eval(idx);
               if(val[TAG_idx] = NBR_tag,
                  { nbr: val[NBR_VAL_idx];
                    if(nbr > 0, 
                       { tab: exp[TAB_TAB_idx];
                         if(nbr > size(tab),
                            error('index beyond size'),
                            tab[nbr]) },
                       error('non-positive index')) },
                  error('invalid index')) },
             error('table required')) };

      eval_multitabulation(Exp):
        { nam: Exp[MTL_NAM_idx];
          exp: get(nam, DCT);
          `display@['received multitabulation: ', Exp, eoln];`
          if(exp[TAG_idx] = MTB_tag,
             { Siz: Exp[MTL_TAB_idx];
               siz: exp[MTB_SIZ_idx];
               if(not (size(siz) = size(Siz)), 
                  error('invalid index'),
                  for(i:1, i:=i+1, not(i>size(Siz)),
                      { val: eval(Siz[i]);
                        if(val[TAG_idx] = NBR_tag,
                           { nbr: val[NBR_VAL_idx];
                             if(nbr < 0,
                                error('non-positive index'),
                                { idx: siz[i];
                                  idx_nbr: idx[NBR_VAL_idx];
                                  if(nbr > idx_nbr,
                                     error('index beyond size'),
                                     Siz[i] := val)})},
                            error('invalid size for multitabulation')) }) );
                            
               `If we reach this point, this means the index is valid`
               `so we should assign the element`
               `Don't forget to evaluate Exp when assigning`   
               ofs: 0;
               dim: exp[MTB_DIM_idx];
               dim := dim[NBR_VAL_idx];
               for(i:1, i:=i+1, not(i>dim),
                   { s: 1;
                     for(j:i+1, j:=j+1, not(j>dim),
                         { idx: siz[j];
                           idx_nbr: idx[NBR_VAL_idx];
                           s:= s*idx_nbr});
                     idx: Siz[i];
                     idx_nbr: idx[NBR_VAL_idx];
                     ofs:= ofs+ (s * (idx_nbr - 1)) });
               ofs := ofs+1;
               `display@['offset for assignment is: ', ofs]`
               dat: exp[MTB_TAB_idx];
               dat[ofs]},
              error('Multidimensional table required'))};
	  

      define_variable(Var, Exp):
        { nam: Var[VAR_NAM_idx];
          val: eval(Exp);
          DCT:= add(nam, val, DCT);
          val };
    
      define_application(Apl, Exp):
        { nam: Apl[APL_NAM_idx];
          arg: Apl[APL_ARG_idx];
          DCT:= add(nam, Void, DCT);
          fun: FUN(nam, arg, Exp, DCT);
          set(nam, fun, DCT);
          fun };
    
      define_tabulation(Tbl, Exp):
        { nam: Tbl[TBL_NAM_idx];
          idx: Tbl[TBL_IDX_idx];
          val: eval(idx);
          if(val[TAG_idx] = NBR_tag,
             { nbr: val[NBR_VAL_idx];
               if(nbr < 0,
                  error('negative size'),
                  { exp: if(nbr > 0,
                            { tab[nbr]: eval(Exp);
                              TAB(tab) },
                            Empty );
                    DCT:= add(nam, exp, DCT);
                    exp }) },
             error('invalid size')) }; 

      define_multitabulation(Tbl, Exp):
        { nam: Tbl[MTL_NAM_idx];
          siz: Tbl[MTL_TAB_idx];
          dat: 1;
          for(i:1, i:=i+1, not(i>size(siz)),
              { val: eval(siz[i]);
                if(val[TAG_idx] = NBR_tag,
                   { nbr: val[NBR_VAL_idx];
                     if(nbr < 0,
                        error('negative dimension'),
                        { siz[i] := val; 
                          dat := dat * nbr }) },
                   error('invalid size')) });
                   
          tbl[dat]: eval(Exp);
          mtb: MTB(NBR(size(siz)), siz, tbl);
          DCT:= add(nam, mtb, DCT);
          `display@['Defined multitable: ', mtb];`
          mtb};
   
      define_error(Inv, Exp):
         error('invocation required');
  
      define_case: case(VAR_tag => define_variable,
                        APL_tag => define_application,
                        TBL_tag => define_tabulation,
                        `Add multitabulation definition`
                        MTL_tag => define_multitabulation,
                           else => define_error);
  
      eval_definition(Exp):
        { inv: Exp[DEF_INV_idx];
          exp: Exp[DEF_EXP_idx];
          tag: inv[TAG_idx];
          `display@['DEBUG: define_case(', tag, ')'];`
          cas: define_case(tag);
          `display@['DEBUG eval_definition: CAS(', inv, ',', exp, ')'];`
          cas(inv, exp) };

      assign_variable(Var, Exp):
        { nam: Var[VAR_NAM_idx];
          val: eval(Exp);
          set(nam, val, DCT);
          val };
    
      assign_application(Apl, Exp):
        { nam: Apl[APL_NAM_idx];
          arg: Apl[APL_ARG_idx];
          fun: FUN(nam, arg, Exp, DCT);
          set(nam, fun, DCT);
          fun };
    
      assign_tabulation(Tbl, Exp):
        { nam: Tbl[TBL_NAM_idx];
          exp: get(nam, DCT);
          if(exp[TAG_idx] = TAB_tag,
             { idx: Tbl[TBL_IDX_idx];
               val: eval(idx);
               if(val[TAG_idx] = NBR_tag,
                  { nbr: val[NBR_VAL_idx];
                    if(nbr > 0, 
                       { tab: exp[TAB_TAB_idx];
                         if(nbr > size(tab),
                            error('index beyond size'),
                            { tab[nbr]:= eval(Exp);
                              exp }) },
                       error('non-positive index')) },
                  error('invalid index')) },
             error('table required')) };

      assign_multitabulation(Tbl, Exp):
        { nam: Tbl[MTL_NAM_idx];
          exp: get(nam, DCT);
          if(exp[TAG_idx] = MTB_tag,
             { Siz: Tbl[MTL_TAB_idx];
               siz: exp[MTB_SIZ_idx];
               if(not (size(siz) = size(Siz)), 
                  error('invalid index'),
                  for(i:1, i:=i+1, not(i>size(Siz)),
                      { val: eval(Siz[i]);
                        if(val[TAG_idx] = NBR_tag,
                           { nbr: val[NBR_VAL_idx];
                             if(nbr < 0,
                                error('non-positive index'),
                                { idx: siz[i];
                                  idx_nbr: idx[NBR_VAL_idx];
                                  if(nbr > idx_nbr,
                                     error('index beyond size'),
                                     Siz[i] := val)})},
                            error('invalid size for multitabulation')) }) );
                            
               `If we reach this point, this means the index is valid`
               `so we should assign the element`
               `Don't forget to evaluate Exp when assigning`     
               
               dim: exp[MTB_DIM_idx];
               dim:= dim[NBR_VAL_idx];       
               ofs: 0;
               for(i:1, i:=i+1, not(i>dim),
                   { s: 1;
                     for(j:i+1, j:=j+1, not(j>dim),
                         { idx: siz[j];
                           idx_nbr: idx[NBR_VAL_idx];
                           s:= s*idx_nbr});
                     idx: Siz[i];
                     idx_nbr: idx[NBR_VAL_idx];
                     ofs:= ofs+ (s * (idx_nbr - 1)) });
               ofs := ofs+1;
               `display@['offset for assignment is: ', ofs]`
               dat: exp[MTB_TAB_idx];
               dat[ofs] := eval(Exp)},
              error('Multidimensional table required'));
          exp};
   
      assign_error(Inv, Exp):
         error('invocation required');
  
      assign_case: case(VAR_tag => assign_variable,
                        APL_tag => assign_application,
                        TBL_tag => assign_tabulation,
                        `Add multitabulation assignment`
                        MTL_tag => assign_multitabulation,
                           else => assign_error);
  
      eval_assignment(Exp):
        { inv: Exp[SET_INV_idx];
          exp: Exp[SET_EXP_idx];
          tag: inv[TAG_idx];
          cas: assign_case(tag);
          `display@['DEBUG eval_assignment(Exp): CAS(', inv, ',', Exp, ')'];`
          cas(inv, exp) };
    
      eval_identity(Exp):
        Exp;
    
      eval_case: case(VAR_tag => eval_variable,
                      APL_tag => eval_application,
                      TBL_tag => eval_tabulation,
                      DEF_tag => eval_definition,
                      SET_tag => eval_assignment,
                      `Add multitabulation evaluation`
                      MTL_tag => eval_multitabulation,
                         else => eval_identity);
  
      init_eval(Dct):=
        { DCT:= Dct;
          void };

      eval(Exp):=
        { tag: Exp[TAG_idx];
          `display@['DEBUG: EVAL_CASE(', tag, ')'];`
          cas: eval_case(tag);
          `display@['DEBUG eval(Exp): CAS(', Exp, ')'];`
          cas(Exp) };
  
      void };

  display('evaluator installed', eoln) }

